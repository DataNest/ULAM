
use SiteUtils;
/**
   Sexual evolution in paint.  Another demo of painting with
   SiteUtils.  Gets the site paint using the SiteUtils.getOut(),
   crosses it with our stored color, then mutates it, and stores the
   result in both the site and in ourselves with SiteUtils.setOut().

   \symbol SX
   \color #2d2
   \symmetries all
   \radius 1
   \author Dave
   \license public domain
 */
element SiteCrosser {
  // Typedefs
  typedef SiteUtils.Channel Channel;
  typedef SiteUtils.ARGB ARGB;

  // Utilities
  SiteUtils su;
  EventWindow ew;
  Random random;

  ARGB m_color = { 0xff, 0x80, 0x80, 0x80}; // Init to grey

  Void behave() {
    // Start with current site color
    ARGB kid = su.getOut();

    // Crossover and mutate with stored color
    for (Int i = 1; i < 4; ++i) {
      Int color = kid[i];

      // Uniform crossover by channels
      if (random.oneIn(2)) color = m_color[i];

      // Mutate
      color += random.between(-2,2);

      // Store
      kid[i] = (Channel) color;
    }

    // Write offspring 1
    su.setOut(kid);

    // And become offspring 2
    m_color = kid;

    // And move on
    ew.swap(0,1);
  }

  ARGB getColor(Unsigned selector) {
    return m_color;
  }
}
