<PROGRAM> := 'ulam' + <CLASS_BLOCK>  + <EOF>
<CLASS_BLOCK> := '{' + <DATA_MEMBERS> + '}'

<DATA_MEMBERS> := ( <FUNC_DEF> | <DECL> + ';' | <TYPE_DEF> + ';' )*

<DECL> := <TYPE> + <VAR_DECLS>
<VAR_DECLS> := <VAR_DECL> | <VAR_DECL> + ',' + <VAR_DECLS>
<VAR_DECL> := <LVAL_EXPRESSION>

<TYPE> := 'Int' | 'Bool' | 'Float' | TYPEDEF

<TYPE_DEF> := 'typedef' + <TYPE> + ( <IDENT> | <IDENT> + '[' + <EXPRESSION> + ']' )

<FUNC_DEF> := <FUNC_DECL> + <BLOCK>
<FUNC_DECL> := <TYPE> + <IDENT> + '(' + <FUNC_PARAMS> + ')'
<FUNC_PARAMS> := 0 | <FUNC_PARAM> | <FUNC_PARAM> + ',' + <FUNC_PARAMS>
<FUNC_PARAM>  := <TYPE> + <VAR_DECL> | <FUNC_DECL>

<BLOCK> := '{' + <STATEMENTS> + '}'

<STATEMENTS> := 0 | <STATEMENT> + <STATEMENTS>
<STATEMENT> := <SIMPLE_STATEMENT> | <CONTROL_STATEMENT> | <BLOCK> 
<SIMPLE_STATEMENT> := ( 0 | <DECL> | <ASSIGN_EXPRESSION> | <TYPE_DEF> ) + ';'


<CONTROL_STATEMENT> := <IF_STATEMENT> | <WHILE_STATEMENT>

<IF_STATEMENT> := 'if' + '(' + <ASSIGN_EXPRESSION> + ') + <STATEMENT> + <OPT_ELSE_STATEMENT>
<OPT_ELSE_STATEMENT> := 0 | 'else' + <STATEMENT>

<WHILE_STATEMENT> := 'while' + '(' + <ASSIGN_EXPRESSION> + ')' + <STATEMENT>


<ASSIGN_EXPRESSION> := <EXPRESSION> | <LVAL_EXPRESSION> + <ASSIGN_OP> + <ASSIGN_EXPRESSION>
<ASSIGN_OP> := '=' | '+=' | '-=' | '*=' | '&=' | '|=' | '^='

<LVAL_EXPRESSION> := <IDENT> | <IDENT> + '[' + <EXPRESSION> + ']' | <FUNC_CALL>

<FUNC_CALL> := <IDENT> + '(' + <ARGS> + ')'
<ARGS> := 0 | <ARG> | <ARG> + ',' + <ARGS>
<ARG>  := <ASSIGN_EXPRESSION>


<EXPRESSION> := <TERM> | <EXPRESSION> <ADDOP> <TERM>
<ADDOP> := '+' | '-' | '&' | '|' | '^'

<TERM> := <FACTOR> | <TERM> <MULOP> <FACTOR>
<MULOP> := '*' | '/'
<FACTOR> := <LVAL_EXPRESSION> | <NUMBER> | '(' + <ASSIGN_EXPRESSION> + ')' | <UNOP> + <FACTOR>
<UNOP> := '-' | '+' | '!'
